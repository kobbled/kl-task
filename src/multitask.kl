PROGRAM multitask
%NOBUSYLAMP
%COMMENT = 'task helper func'
%NOLOCKGROUP
%NOPAUSE = COMMAND + TPENABLE + ERROR

%include errors.klt
%include multitask.klt

%include errors.klh
%include multitask.klh


ROUTINE task__thread
  VAR
    status : INTEGER
  BEGIN
    RUN_TASK(task_name, 0, FALSE, FALSE, 0, status)

    IF status <> 0 THEN
      karelError(status, 'Problem staring task: ' + task_name, ER_WARN)
      RETURN(FALSE)
    ENDIF

    RETURN(TRUE)
  
  END task__thread


ROUTINE task__thread_motion
  VAR
    status : INTEGER
  BEGIN
    --group mask decimal corresponds with (gp1,gp2,gp3,gp4)
        --1000 = 1 : 0100 = 2  : 0010 = 4  : 0001 = 8
        --           1100 = 3  : 1010 = 5  : 1001 = 9
        --           1110 = 7  : 1110 = 7  : 1011 = 13
        --                     : 1011 = 13 :
    RUN_TASK(task_name, 0, FALSE, TRUE, grp_mask, status)

    IF status <> 0 THEN
      karelError(status, 'Problem staring task: ' + task_name, ER_WARN)
      RETURN(FALSE)
    ENDIF

    RETURN(TRUE)
  
  END task__thread_motion

ROUTINE task__wait
  VAR
    status, n : INTEGER
    task_no : INTEGER
    attr_out : INTEGER
    dummy : STRING[2]
  BEGIN
    --initialize status check counter
    n = 0
    --wait for task to pause or abort
    CHECK_JOIN::

    GET_TSK_INFO(task_name, task_no, TSK_STATUS, attr_out, dummy, status)
    IF status <> 0 THEN
      karelError(status,'problem in task wait getting status of ' + task_name + '', ER_ABORT)
      RETURN(FALSE)
    ENDIF

    IF attr_out = PG_RUNNING THEN
      DELAY MAX_CHECK_DELAY
      n = n + 1
      IF (n < MAX_STATUS_CHECKS) THEN GOTO CHECK_JOIN; ENDIF
      karelError(PROGRAM_TIMEDOUT, 'task ' + task_name + ' could not complete in the within' + & 
                 'the maxium timeout set for wait', ER_ABORT)
    ENDIF

    -- once task is completed pause task

    PAUSE_TASK(task_name, TRUE, TRUE, status)
    IF status <> 0 THEN
      karelError(status,'thread barrier for ' + task_name + ' was not successful', ER_WARN)
      RETURN(FALSE)
    ENDIF

    RETURN(TRUE)
  END task__wait


ROUTINE task__is_task_done
  VAR 
    status : INTEGER
    task_no : INTEGER
    attr_out : INTEGER
    dummy : STRING[2]
  BEGIN
  
    GET_TSK_INFO(task_name, task_no, TSK_STATUS, attr_out, dummy, status)
    IF (attr_out = PG_ABORTED) OR (attr_out = PG_ABORTING) THEN
      RETURN(TRUE)
    ENDIF

    RETURN(FALSE)

  END task__is_task_done

ROUTINE task__is_task_running
  VAR 
    status : INTEGER
    task_no : INTEGER
    attr_out : INTEGER
    dummy : STRING[2]
  BEGIN
  
    GET_TSK_INFO(task_name, task_no, TSK_STATUS, attr_out, dummy, status)
    IF (attr_out = PG_RUNNING) THEN
      RETURN(TRUE)
    ENDIF

    RETURN(FALSE)

  END task__is_task_running

ROUTINE task__abort
  VAR
    status : INTEGER
  BEGIN
    ABORT_TASK(task_name, TRUE, FALSE, status)
    IF status <> 0 THEN
      karelError(status, task_name + ' was not aborted successfully', ER_WARN)
      RETURN(FALSE)
    ELSE
      RETURN(TRUE)
    ENDIF
  END task__abort

ROUTINE task__abort_motion
  VAR
    status : INTEGER
  BEGIN
    ABORT_TASK(task_name, TRUE, TRUE, status)
    IF status <> 0 THEN
      karelError(status, task_name + ' was not aborted successfully', ER_WARN)
      RETURN(FALSE)
    ELSE
      RETURN(TRUE)
    ENDIF
  END task__abort_motion

BEGIN
END multitask
